import io.franzbecker.gradle.lombok.task.DelombokTask
import org.apache.tools.ant.filters.ReplaceTokens
import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        mavenCentral()
    }
 
    dependencies {
        classpath "net.sf.proguard:proguard-gradle:5.3.3"
    }
}

plugins {
    id "io.franzbecker.gradle-lombok" version "1.8" apply false
}

allprojects {
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "maven-publish"
    apply plugin: "signing"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "io.franzbecker.gradle-lombok"

    group = "dev.aura.bungeechat"
    version = "2.0.0"
    
    ext {
        build = "130"
        full_version = "${version}_${build}"
        shouldSign = project.hasProperty("signing.keyId") && project.hasProperty("signing.password") && project.hasProperty("signing.secretKeyRingFile")
    }
    
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    
    repositories {
        jcenter()
        mavenCentral()
    }
}

description = """Bungee Chat"""

configurations {
    reTrace
}

repositories {
    maven {
        name "bungeecord-repo"
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        name "bungeeperms-repo"
        url "http://repo.wea-ondara.net/repository/public/"
    }
    maven {
        name "jitpack.io"
        url "https://jitpack.io"
    }
    maven {
        name "luck-repo"
        url "https://repo.lucko.me/"
    }
}

dependencies {
    compileOnly project(":BungeeChatApi")
    
    compileOnly "net.md-5:bungeecord-api:${bungeecoord_version}"
    
    compileOnly "net.alpenblock:BungeePerms:${bungeeperms_version}"
    compileOnly "com.github.CheeseSoftware:PowerfulPermsAPI:${powerfulperms_version}"
    compileOnly "me.lucko.luckperms:luckperms-api:${luckperms_version}"
    
    reTrace "net.sf.proguard:proguard-retrace:5.3.3"
}

allprojects {
    lombok {
        version = lombok_version
        sha256 = lombok_hash
    }
}

allprojects {
    processResources {
        // this will ensure that this task is redone when the versions change.
        inputs.property "version", version
        inputs.property "build", build
        
        expand "version": version, "build": build
    }

    task processSources (type: Sync) {
        inputs.property "version", version
        inputs.property "build", build
        ext.outputDir = file("$buildDir/generated-src")
        
        from sourceSets.main.allSource
        into outputDir
        filter (ReplaceTokens, tokens: [
            "version": version,
            "build": build
        ])
    }
    
    task delombok(type: DelombokTask, dependsOn: processSources) {
        group "build"
        description "Generates Lombok free Java code from code with Lombok"
    
      	ext.outputDir = file("$buildDir/delombok")
        
      	outputs.dir(outputDir)
    		inputs.dir(processSources.outputDir)
    		args(processSources.outputDir, "-d", outputDir)
        
        compileConfigurationName = configurations.compileOnly.name
    }
    
    compileJava {
        source = processSources.outputs
        dependsOn processSources 
    }
    
    jar {
        from sourceSets.main.output
        
        version = full_version
    }
    
    task sourceJar (type: Jar, dependsOn: delombok) {
        group "build"
    
        from delombok.outputDir
    
        classifier = "sources"
        version = jar.version
    }
}

jar {
    from project(":BungeeChatApi").sourceSets.main.output
    
    classifier = "deobf"
}

task proguard(type: ProGuardTask, dependsOn: jar) {
    group "build"

    ext.outputJar = "${buildDir}/libs/${archivesBaseName}-${full_version}.jar"
    
    configuration "proguard.conf"

    // Inputs and outputs
    injars jar.archivePath
    outjars outputJar
    printmapping "${outputJar}.map"
 
    // Libraries
    libraryjars configurations.compileOnly
    
    // Stuff that depends on the group name
    keep "class ${project.group}.api.** { *; }"
    repackageclasses project.group
    
    // No notes or warnings from classes that aren't ours!
    dontwarn "!class ${project.group}.**"
    dontnote "!class ${project.group}.**"
    
    jar.finalizedBy proguard
}

artifacts {
    archives file(proguard.ext.outputJar)
}

/*// Remove default jar from artifacts (We don't want in the maven repo!)
configurations.archives.artifacts.with { archives ->
    def toRemove = new ArrayList()
    
    archives.each {
        if (it.classifier == "deobf")
            toRemove.add(it)
    }
    
    toRemove.each {
        remove(it)
    }
}*/

allprojects {
    signing {
        required { shouldSign }
    
        sign configurations.archives
    }
    
    uploadArchives {
        repositories {
            mavenDeployer {
                if (shouldSign) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }
                if (project.hasProperty("local_maven")) {
                    repository(url: "file://${local_maven}")
                } else {
                    repository(url: "file://${rootProject.projectDir}/maven")
                }
                pom {
                    groupId = project.group
                    version = project.full_version
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging "jar"
                        description project.description
                        url "https://gitlab.brainstonemod.com/shawn-ian/BungeeChat"
                        scm {
                            url "https://gitlab.brainstonemod.com/shawn-ian/BungeeChat"
                            connection "scm:git:git@gitlab.brainstonemod.com:shawn-ian/BungeeChat.git"
                            developerConnection "scm:git:gitlab.brainstonemod.com:shawn-ian/BungeeChat.git"
                        }
                        issueManagement {
                            system "gitlab"
                            url "https://gitlab.brainstonemod.com/shawn-ian/BungeeChat/issues"
                        }
                        licenses {
                            license {
                                name "License"
                                url "???"
                                distribution "repo"
                            }
                        }
                        developers {
                            developer {
                                id "shawn_ian"
                                name "shawn_ian"
                                roles {
                                    role "owner"
                                    role "developer"
                                }
                            }
                            developer {
                                id "brainstone"
                                name "The_BrainStone"
                                roles {
                                    role "developer"
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

task reTrace(type: JavaExec) {
    group "help"
    description """Deobfuscates an obfuscated stacktrace
Specify "-Pmapping_file=<file>" (with quotes) to use a given mapping file. If not provided use (and generate if necessary) the mapping file of the current code.
Specify "-Pstacktrace_file=<file>" (with quotes) to specify the stacktrace file. If not provided "stacktrace.txt" (in the project root) is used."""

    classpath configurations.reTrace
    main = "proguard.retrace.ReTrace"
    
    def mapping_file
    def stacktrace_file
    
    if (project.hasProperty("mapping_file")) {
        mapping_file = project.mapping_file
    } else {
        dependsOn proguard
        
        mapping_file = "${proguard.outputJar}.map"
    }
    
    if (project.hasProperty("stacktrace_file")) {
        stacktrace_file = project.stacktrace_file
    } else {
        stacktrace_file = "stacktrace.txt"
    }
    
    args mapping_file
    args stacktrace_file
}
