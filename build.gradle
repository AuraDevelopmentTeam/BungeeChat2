import io.franzbecker.gradle.lombok.task.DelombokTask
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
        id "io.franzbecker.gradle-lombok" version "1.8" apply false
    }

allprojects {
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "maven-publish"
    apply plugin: "signing"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "io.franzbecker.gradle-lombok"

    group = "dev.aura.bungeechat"
    version = "2.0.0"
    
    ext {
        build = "130"
        full_version = "${version}_${build}"
        shouldSign = project.hasProperty("signing.keyId") && project.hasProperty("signing.password") && project.hasProperty("signing.secretKeyRingFile")
    }
    
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    
    repositories {
        jcenter()
        mavenCentral()
    }
}

description = """Bungee Chat"""

repositories {
    maven {
        name "bungeecord-repo"
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        name "bungeeperms-repo"
        url "http://repo.wea-ondara.net/repository/public/"
    }
    maven {
        name "jitpack.io"
        url "https://jitpack.io"
    }
    maven {
        name "luck-repo"
        url "https://repo.lucko.me/"
    }
}

dependencies {
    compile project(":BungeeChatApi")
    
    compile "net.md-5:bungeecord-api:${bungeecoord_version}"
    
    compile "net.alpenblock:BungeePerms:${bungeeperms_version}"
    compile "com.github.CheeseSoftware:PowerfulPermsAPI:${powerfulperms_version}"
    compile "me.lucko.luckperms:luckperms-api:${luckperms_version}"
}

allprojects {
    lombok {
        version = lombok_version
        sha256 = lombok_hash
    }
}

allprojects {
    processResources {
        // this will ensure that this task is redone when the versions change.
        inputs.property "version", version
        inputs.property "build", build
        
        expand "version": version, "build": build
    }

    task processSources (type: Sync) {
        inputs.property "version", version
        inputs.property "build", build
        ext.outputDir = file("$buildDir/generated-src")
        
        from sourceSets.main.allSource
        into outputDir
        filter (ReplaceTokens, tokens: [
            "version": version,
            "build": build
        ])
    }
    
    task delombok(type: DelombokTask, dependsOn: processSources) {
      	ext.outputDir = file("$buildDir/delombok")
        
      	outputs.dir(outputDir)
    		inputs.dir(processSources.outputDir)
    		args(it, "-d", outputDir)
    }
    
    compileJava {
        source = processSources.outputs
        dependsOn processSources 
    }
    
    jar {
        from sourceSets.main.output
        
        version = full_version
    }
    
    task sourceJar (type: Jar) {
        from delombok.outputDir
    
        classifier = "sources"
        version = jar.version
        
        dependsOn delombok
    }
}

jar {
    from project(":BungeeChatApi").sourceSets.main.output
}

artifacts {
    archives jar
}

allprojects {
    signing {
        required { shouldSign }
    
        sign configurations.archives
    }
    
    uploadArchives {
        repositories {
            mavenDeployer {
                if (shouldSign) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }
                if (project.hasProperty("local_maven")) {
                    repository(url: "file://${local_maven}")
                } else {
                    repository(url: "file://${rootProject.projectDir}/maven")
                }
                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging "jar"
                        description project.description
                        url "https://gitlab.brainstonemod.com/shawn-ian/BungeeChat"
                        scm {
                            url "https://gitlab.brainstonemod.com/shawn-ian/BungeeChat"
                            connection "scm:git:git@gitlab.brainstonemod.com:shawn-ian/BungeeChat.git"
                            developerConnection "scm:git:gitlab.brainstonemod.com:shawn-ian/BungeeChat.git"
                        }
                        issueManagement {
                            system "gitlab"
                            url "https://gitlab.brainstonemod.com/shawn-ian/BungeeChat/issues"
                        }
                        licenses {
                            license {
                                name "License"
                                url "???"
                                distribution "repo"
                            }
                        }
                        developers {
                            developer {
                                id "shawn_ian"
                                name "shawn_ian"
                                roles {
                                    role "owner"
                                    role "developer"
                                }
                            }
                            developer {
                                id "brainstone"
                                name "The_BrainStone"
                                roles {
                                    role "developer"
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
